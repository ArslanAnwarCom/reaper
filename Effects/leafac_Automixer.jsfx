desc:leafac_Automixer

// https://github.com/leafac/reaper

/*
Video processor:

// Automixer
//@param 1:instance_identifier 'Instance identifier' 0 0 50 0 1
//@gmem=leafac_Automixer

gfx_blit(input_track(gmem[instance_identifier]));
*/

slider1:mode=0<0,2,1{Single-track output,Multi-track output,Bypassed}>Â 

slider3:channels_per_track=2<2,32,2>Channels per track
slider4:rms_size_ms=300<0,1000,.1>Reaction time (RMS size in ms)
slider5:strength=1<0,5,.01>Strength
slider6:lookahead_ms=0<0,1000,.1>Lookahead (ms)

slider15:instance_identifier=0<0,50,1>-Instance identifier

slider20:track_gain_maximum_difference_db=-20<-60,0,.1>-Track gain maximum difference (dB)
slider21:track_gain_maximum_debounce_ms=350<0,1000,.1>-Track gain maximum reaction time (ms)

slider33:1<0,10,0.1>Track 1 priority
slider34:1<0,10,0.1>Track 2 priority
slider35:1<0,10,0.1>Track 3 priority
slider36:1<0,10,0.1>Track 4 priority
slider37:1<0,10,0.1>-Track 5 priority
slider38:1<0,10,0.1>-Track 6 priority
slider39:1<0,10,0.1>-Track 7 priority
slider40:1<0,10,0.1>-Track 8 priority
slider41:1<0,10,0.1>-Track 9 priority
slider42:1<0,10,0.1>-Track 10 priority
slider43:1<0,10,0.1>-Track 11 priority
slider44:1<0,10,0.1>-Track 12 priority
slider45:1<0,10,0.1>-Track 13 priority
slider46:1<0,10,0.1>-Track 14 priority
slider47:1<0,10,0.1>-Track 15 priority
slider48:1<0,10,0.1>-Track 16 priority
slider49:1<0,10,0.1>-Track 17 priority
slider50:1<0,10,0.1>-Track 18 priority
slider51:1<0,10,0.1>-Track 19 priority
slider52:1<0,10,0.1>-Track 20 priority
slider53:1<0,10,0.1>-Track 21 priority
slider54:1<0,10,0.1>-Track 22 priority
slider55:1<0,10,0.1>-Track 23 priority
slider56:1<0,10,0.1>-Track 24 priority
slider57:1<0,10,0.1>-Track 25 priority
slider58:1<0,10,0.1>-Track 26 priority
slider59:1<0,10,0.1>-Track 27 priority
slider60:1<0,10,0.1>-Track 28 priority
slider61:1<0,10,0.1>-Track 29 priority
slider62:1<0,10,0.1>-Track 30 priority
slider63:1<0,10,0.1>-Track 31 priority
slider64:1<0,10,0.1>-Track 32 priority

options:no_meter gmem=leafac_Automixer

@init

MODE_SINGLE_TRACK_OUTPUT = 0;
MODE_MULTI_TRACK_OUTPUT = 1;
MODE_BYPASSED = 2;

@slider

tracks_maximum = 64 / 2;
tracks = num_ch / channels_per_track;
rms_size_samples = max(1, ceil(srate * rms_size_ms / 1000));
rms_sample_counter = -1;
lookahead_samples = max(1, ceil(srate * lookahead_ms / 1000));
lookahead_sample_counter = -1;
pdc_delay = lookahead_samples;
pdc_bot_ch = 0;
pdc_top_ch = num_ch;
track_gain_maximum_difference_amplitude = 10^(track_gain_maximum_difference_db / 20);
track_gain_maximum_debounce_samples = max(1, ceil(srate * track_gain_maximum_debounce_ms / 1000));

address = 0;
tracks_priorities = address; address += tracks_maximum;
track = -1; loop(tracks_maximum, track += 1;
  tracks_priorities[track] = slider(33 + track);
);
tracks_samples = address; address += tracks;
track = -1; loop(tracks, track += 1;
  tracks_samples[track] = address; address += channels_per_track;
  channel = -1; loop(channels_per_track, channel += 1;
    tracks_samples[track][channel] = address; memset(address, 0, lookahead_samples); address += lookahead_samples;
  );
);
tracks_squares = address; address += tracks;
track = -1; loop(tracks, track += 1;
  tracks_squares[track] = address; memset(address, 0, rms_size_samples); address += rms_size_samples;
);
tracks_sums_squares = address; memset(address, 0, tracks); address += tracks;
tracks_rmss = address; memset(address, 0, tracks); address += tracks;
tracks_gains_computation = address; memset(address, 0, tracks); address += tracks;
tracks_gains = address; memset(address, 0, tracks); address += tracks;
tracks_rmss_gained = address; memset(address, 0, tracks); address += tracks;
freembuf(address + 1);

@sample

rms_sample_counter += 1; rms_sample_counter == rms_size_samples ? rms_sample_counter -= rms_size_samples;
lookahead_sample_counter += 1; lookahead_sample_counter == lookahead_samples ? lookahead_sample_counter -= lookahead_samples;

sum_of_tracks_rmss = 0;
track = -1; loop(tracks, track += 1;
  track_square = 0;
  channel = -1; loop(channels_per_track, channel += 1;
    track_square += sqr(spl(track * channels_per_track + channel));
  );
  track_square /= channels_per_track;
  track_sum_squares =
    tracks_sums_squares[track] -
    tracks_squares[track][rms_sample_counter] +
    track_square;
  track_rms = sqrt(track_sum_squares / rms_size_samples);

  tracks_squares[track][rms_sample_counter] = track_square;
  tracks_sums_squares[track] = track_sum_squares;
  sum_of_tracks_rmss += tracks_rmss[track] = track_rms;
);

sum_of_tracks_gains = 0;
track = -1; loop(tracks, track += 1;
  track_gain = tracks_rmss[track] / sum_of_tracks_rmss * tracks_priorities[track];

  sum_of_tracks_gains += tracks_gains_computation[track] = track_gain;
);

previous_sum_of_tracks_gains = sum_of_tracks_gains;
sum_of_tracks_gains = 0;
track = -1; loop(tracks, track += 1;
  track_gain = tracks_gains_computation[track] / previous_sum_of_tracks_gains;
  track_gain = track_gain^strength / (track_gain^strength + (1 - track_gain)^strength);

  sum_of_tracks_gains += tracks_gains_computation[track] = track_gain;
);

track_gain_maximum_track = -1;
track_gain_maximum_gain = -1;
track = -1; loop(tracks, track += 1;
  track_gain = tracks_gains_computation[track] / sum_of_tracks_gains;
  mode == MODE_BYPASSED ? track_gain = 1;
  track_rms_gained = tracks_rmss[track] * track_gain;

  tracks_gains[track] = track_gain;
  tracks_rmss_gained[track] = track_rms_gained;

  channel = -1; loop(channels_per_track, channel += 1;
    sample = tracks_samples[track][channel][lookahead_sample_counter];
    tracks_samples[track][channel][lookahead_sample_counter] = spl(track * channels_per_track + channel);
    mode == MODE_MULTI_TRACK_OUTPUT ? (
      spl(track * channels_per_track + channel) = sample * track_gain;
    ) : (
      spl(track * channels_per_track + channel) = 0;
      spl(channel) += sample * track_gain;
    );
  );

  track_gain_maximum_gain < track_gain ? (
    track_gain_maximum_track = track;
    track_gain_maximum_gain = track_gain;
  );
);
abs(track_gain_maximum_gain - tracks_gains[gmem[instance_identifier]]) < track_gain_maximum_difference_amplitude ? (
  track_gain_maximum_debounce = track_gain_maximum_debounce_samples;
) : (
  track_gain_maximum_debounce -= 1;
  track_gain_maximum_debounce == 0 ? gmem[instance_identifier] = track_gain_maximum_track;
);

@serialize

version = 200;
file_var(0, version);

@gfx 530 180

gfx_set(1);
gfx_center_horizontally = 1;
gfx_center_vertically = 4;

meter_width = 10;
meter_height = 100;
meter_minimum_value = -62;

title_height = 30;

track_width = meter_width * 5;
track_height = meter_height + title_height;
track_padding = 30;

legend_padding_left = 10;
legend_padding_bottom = 15;

columns = max(1, floor((gfx_w - track_padding) / (track_width + track_padding)));

track_gfx = -1; loop(tracks, track_gfx += 1;
  track_x = track_gfx % columns;
  track_y = floor(track_gfx / columns);
  track_left = (track_padding + track_width) * track_x + track_padding;
  track_top = (track_padding + track_height) * track_y + track_padding;
  track_right = track_left + track_width;
  track_meters_bottom = track_top + meter_height;
  track_bottom = track_top + track_height;

  gfx_x = track_left;
  gfx_y = track_meters_bottom;
  gfx_rectto(
    gfx_x + meter_width,
    gfx_y - max(1, min(meter_height,
      meter_height * (meter_minimum_value - 20 * log10(tracks_rmss[track_gfx])) / meter_minimum_value
    ))
  );

  gfx_x = track_left + meter_width * 2.5;
  gfx_y = track_meters_bottom;
  gfx_lineto(gfx_x, gfx_y - meter_height);
  gfx_x = track_left + meter_width * 2;
  gfx_y = track_meters_bottom - meter_height * tracks_gains[track_gfx];
  gfx_lineto(gfx_x + meter_width, gfx_y);

  gfx_x = track_left + meter_width * 4;
  gfx_y = track_meters_bottom;
  gfx_rectto(
    gfx_x + meter_width,
    gfx_y - max(1, min(meter_height,
      meter_height * (meter_minimum_value - 20 * log10(tracks_rmss_gained[track_gfx])) / meter_minimum_value
    ))
  );
  
  gfx_x = track_left;
  gfx_y = track_meters_bottom;
  sprintf(#title, "%d", track_gfx + 1);
  track_gfx == gmem[instance_identifier] ? gfx_set(1, 0, 0);
  gfx_drawstr(#title, gfx_center_horizontally | gfx_center_vertically, track_right, track_bottom);
  gfx_set(1);
);

gfx_set(1, 0, 0);
gfx_x = legend_padding_left;
gfx_y = gfx_h - legend_padding_bottom;
gfx_drawstr("Video track");
