// https://youtu.be/dJnABqy_kB0

options:no_meter

slider1:is_paused=0<0,1,1{Play,Pause}>Â 

slider3:gfx_samples_size=144000<144000,10,1>Zoom horizontally (Number of samples)
slider4:amplitude_multiplication_factor=1<0,20,.01>Zoom vertically (Amplitude multiplication factor)

slider6:gfx_samples_offset=0<144000,0,1>Move horizontally (Number of samples)
slider7:move_vertically_factor=0<-1,1,.01>Move vertically (Amplitude multiplication factor)

@init

samples = 0;
samples_size = 48000 * 3 * 2;
freembuf(samples_size + 1);

samples_counter = -1;

old_is_paused = is_paused;

@slider

old_is_paused == 1 && is_paused == 0 ? memset(samples, 0, samples_size); old_is_paused = is_paused;

@sample

!is_paused ? (
  samples_counter += 1; samples_counter == samples_size ? samples_counter -= samples_size;

  // FIXME: Maybe look at all the channels.
  samples[samples_counter] = spl0;
);

@gfx

gfx_h_middle = gfx_h/2;

gfx_set(0.5);
gfx_line(0, gfx_h_middle - gfx_h_middle * move_vertically_factor, gfx_w, gfx_h_middle - gfx_h_middle * move_vertically_factor);

gfx_set(1);
gfx_x = gfx_w;
gfx_samples_counter = samples_counter - gfx_samples_offset; gfx_samples_counter <= -1 ? gfx_samples_counter += samples_size;
loop(gfx_samples_size, gfx_samples_counter -= 1; gfx_samples_counter == -1 ? gfx_samples_counter += samples_size;
  gfx_lineto(
    gfx_x - gfx_w / gfx_samples_size,
    gfx_h_middle - gfx_h_middle * move_vertically_factor - samples[gfx_samples_counter] * gfx_h_middle * amplitude_multiplication_factor
  );
);

