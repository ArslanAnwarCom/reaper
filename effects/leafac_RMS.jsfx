// https://youtu.be/mnr3udsFZO0
// https://youtu.be/IhFqOe29X90
// https://youtu.be/f12dXQ6Rx_8

slider1:window_size_ms=300<0,1000,.1>Window size (ms)

slider3:rms_db_definitional=0<-62,6,.1>RMS Definitional (dB)
slider4:rms_db_optimized=0<-62,6,.1>RMS Optimized (dB)
slider5:rms_db_approximated=0<-62,6,.1>RMS Approximated (dB)

@slider

window_size_samples = max(1, srate * window_size_ms / 1000);

window_pointer = -1;

// Definitional
window_definitional = 0;

// Optimized
window_optimized = window_size_samples;
window_sum_optimized = 0;

freembuf(window_size_samples * 2 + 1);

// Approximated
moving_average_approximated = 0;
moving_average_coefficient_approximated = 1 - exp(-1/window_size_samples);

@sample

window_pointer += 1; window_pointer == window_size_samples ? window_pointer -= window_size_samples;

// FIXME: Look at all the channels.

// Definitional
window_definitional[window_pointer] = spl0;

// Optimized
window_sum_optimized -= window_optimized[window_pointer];
window_sum_optimized += window_optimized[window_pointer] = sqr(spl0);
rms_amplitude_optimized = sqrt(window_sum_optimized / window_size_samples);
rms_db_optimized = 20 * log10(rms_amplitude_optimized);
sliderchange(slider4);

// Approximated
moving_average_approximated += moving_average_coefficient_approximated * (sqr(spl0) - moving_average_approximated);
rms_amplitude_approximated = sqrt(moving_average_approximated);
rms_db_approximated = 20 * log10(rms_amplitude_approximated);
sliderchange(slider5);

@gfx

// Definitional
window_sum_definitional = 0;
window_index_definitional = -1; loop(window_size_samples, window_index_definitional += 1;
  window_sum_definitional += sqr(window_definitional[window_index_definitional]);
);
rms_amplitude_definitional = sqrt(window_sum_definitional / window_size_samples);
rms_db_definitional = 20 * log10(rms_amplitude_definitional);
sliderchange(slider3);
